load cpx.maude

fmod DIRAC is
    sorts Ket Bra .

    --- basis vectors
    ops |0> |1> : -> Ket [ctor] .
    --- basis covectors
    ops <0| <1| : -> Bra [ctor] .
endfm

fmod MAT is
    pr CPX .
    pr DIRAC .

    sorts ZeroV Vect ZeroCV CoVect ZeroM Mat .
    subsorts ZeroV Ket < Vect .
    subsorts ZeroM < Mat .
    subsorts ZeroCV Bra < CoVect .

    --- zero vector, covector and matrix
    op O   : -> ZeroV [ctor] .
    op O^+ : -> ZeroCV [ctor] .
    op mO  : -> ZeroM [ctor] .
    --- abbreviations for basic matrices
    ops |0><0| |0><1| |1><0| |1><1| : -> Mat .
    eq |0><0| = |0> x <0| .
    eq |0><1| = |0> x <1| .
    eq |1><0| = |1> x <0| .
    eq |1><1| = |1> x <1| .
    --- identity matrix
    op I : -> Mat [ctor] .
    --- scalar multiplication with matrix
    op _._ : Cpx Vect -> Vect [prec 31] .
    op _._ : Cpx CoVect -> CoVect [prec 31] .
    op _._ : Cpx Mat -> Mat [prec 31] .
    --- matrix addition
    op _+_ : Vect Vect -> Vect [comm assoc prec 33] .
    op _+_ : CoVect CoVect -> CoVect [comm assoc prec 33] .
    op _+_ : Mat Mat -> Mat [comm assoc prec 33] .
    --- matrix product
    op _x_ : Mat Vect -> Vect [prec 31] .
    op _x_ : Vect CoVect -> Mat [prec 31] .
    op _x_ : Mat Mat -> Mat [assoc prec 31] .
    --- tensor product
    op _(x)_ : Vect Vect -> Vect [assoc prec 31] .
    op _(x)_ : CoVect CoVect -> CoVect [assoc prec 31] .
    op _(x)_ : Mat Mat -> Mat [assoc prec 31] .
    --- dagger
    op (_)^+ : Vect -> CoVect [prec 30] .
    op (_)^+ : CoVect -> Vect [prec 30] .
    op (_)^+ : Mat -> Mat [prec 30] .
    --- inner product
    op <_,_> : Vect Vect -> Cpx .

    vars M M1 M2 M3 M4 : Mat .
    vars V V1 V2 V3 : Vect .
    vars CV CV1 CV2 CV3 : CoVect .
    vars C C1 C2 : Cpx .
    vars K K1 K2 : Ket .
    vars B B1 B2 : Bra .

    --- inner product
    eq < V, O > = 0 .
    eq < O, V > = 0 .
    eq < K1, K2 > = if K1 == K2 then 1 else 0 fi .
    eq < C1 . V1, V2 > = (C1)^* .* < V1, V2 > .
    eq < V1, C2 . V2 > = C2 .* < V1, V2 > .
    eq < V1 + V2, V3 > = < V1, V3 > .+ < V2, V3 > .
    eq < V1, V2 + V3 > = < V1, V2 > .+ < V1, V3 > .
    eq < K1 (x) V1, K2 (x) V2 > = < K1, K2 > .* < V1, V2 > .
    eq (< V1, V2 >)^* = < V2, V1 > .

    --- zero vector
    eq [scalar-O1] : 0 . V = O .
    eq [scalar-O2] : C . O = O .
    eq [add-O1] : O + V = V .
    eq [mul-O1] : M x O = O .
    eq [mul-O2] : mO x V = O .
    eq [tensor-O1] : V (x) O = O .
    eq [tensor-O2] : O (x) V = O .

    --- zero covector
    eq [d-scalar-O1] : 0 . CV = O^+ .
    eq [d-scalar-O2] : C . O^+ = O^+ .
    eq [d-add-O1] : O^+ + CV = CV .
    eq [d-mul-O1] : V x O^+ = mO .
    eq [d-mul-O2] : O x CV = mO .
    eq [d-tensor-O1] : CV (x) O^+ = O^+ .
    eq [d-tensor-O2] : O^+ (x) CV = O^+ .

    --- zero matrix
    eq [m-scalar-O1] : 0 . M = mO .
    eq [m-scalar-O2] : C . mO = mO .
    eq [m-add-O1] : M + mO = M .
    eq [m-mul-O1] : M x mO = mO .
    eq [m-mul-O2] : mO x M = mO .
    eq [m-tensor-O1] : mO (x) M = mO .
    eq [m-tensor-O2] : M (x) mO = mO .

    --- identity matrix properties
    eq [mul-I1] : I x M = M .
    eq [mul-I2] : M x I = M .

    --- dagger properties
    eq [dagger-V1] : (O)^+ = O^+ .
    eq [dagger-V2] : (|0>)^+ = <0| .
    eq [dagger-V3] : (|1>)^+ = <1| .
    eq [dagger-V4] : (C . V)^+ = ((C)^*) . (V)^+ .
    eq [dagger-V5] : (V1 + V2)^+ = (V1)^+ + (V2)^+ .
    eq [dagger-V6] : (V1 (x) V2)^+ = (V1)^+ (x) (V2)^+ .
    eq [dagger-V7] : ((V)^+)^+ = V .

    eq [dagger-CV1] : (O^+)^+ = O .
    eq [dagger-CV2] : (<0|)^+ = |0> .
    eq [dagger-CV3] : (<1|)^+ = |1> .
    eq [dagger-CV4] : (C . CV)^+ = ((C)^*) . (CV)^+ .
    eq [dagger-CV5] : (CV1 + CV2)^+ = (CV1)^+ + (CV2)^+ .
    eq [dagger-CV6] : (CV1 (x) CV2)^+ = (CV1)^+ (x) (CV2)^+ .
    eq [dagger-CV7] : ((CV)^+)^+ = CV .
    
    eq [dagger-M1] : (mO)^+ = mO .
    eq [dagger-M2] : (I)^+ = I .
    eq [dagger-M3] : (K x B)^+ =  (B)^+ x (K)^+ .
    eq [dagger-M4] : (C . M)^+ = ((C)^*) . (M)^+ .
    eq [dagger-M5] : (M1 + M2)^+ = (M1)^+ + (M2)^+ .
    eq [dagger-M6] : (M1 x M2)^+ = (M2)^+ x (M1)^+ .
    eq [dagger-M7] : (M1 (x) M2)^+ = (M1)^+ (x) (M2)^+ .
    eq [dagger-M8] : ((M)^+)^+ = M .

    --- multiplication distributes over addition
    eq [dist-mul-V1] : M x (V1 + V2) = M x V1 + M x V2 .
    eq [dist-mul-V2] : (M1 + M2) x V = M1 x V + M2 x V .
    
    eq [dist-mul-CV1] : V x (CV1 + CV2) = V x CV1 + V x CV2 .
    eq [dist-mul-CV2] : (V1 + V2) x CV = V1 x CV + V2 x CV .

    eq [dist-mul-M1] : M1 x (M2 + M3) = M1 x M2 + M1 x M3 .
    eq [dist-mul-M2] : (M1 + M2) x M3 = M1 x M3 + M2 x M3 .

    --- scalar normal forms
    eq [scalar-V1] : 1 . V = V .
    eq [scalar-V2] : C1 . (C2 . V) = (C1 .* C2) . V .
    eq [scalar-V3] : C . (V1 + V2) = C . V1 + C . V2 .

    eq [scalar-CV1] : 1 . CV = CV .
    eq [scalar-CV2] : C1 . (C2 . CV) = (C1 .* C2) . CV .
    eq [scalar-CV3] : C . (CV1 + CV2) = C . CV1 + C . CV2 .

    eq [scalar-M1] : 1 . M = M .
    eq [scalar-M2] : C1 . (C2 . M) = (C1 .* C2) . M .
    eq [scalar-M3] : C . (M1 + M2) = C . M1 + C . M2 .

    --- additive normal forms
    eq [add-V1] : V + V = 2 . V .
    eq [add-V2] : (C . V) + V = (C .+ 1) . V .
    eq [add-V3] : (C1 . V) + (C2 . V) = (C1 .+ C2) . V .
    
    eq [add-CV1] : CV + CV = 2 . CV .
    eq [add-CV2] : (C . CV) + CV = (C .+ 1) . CV .
    eq [add-CV3] : (C1 . CV) + (C2 . CV) = (C1 .+ C2) . CV .

    eq [add-M1] : M + M = 2 . M .
    eq [add-M2] : (C . M) + M = (C .+ 1) . M .
    eq [add-M3] : (C1 . M) + (C2 . M) = (C1 .+ C2) . M .
    
    --- multiplicative normal forms
    eq [mul-MV1] : (C . M) x V = C . (M x V) .
    eq [mul-MV2] : M x (C . V) = C . (M x V) .
    --- eq [mul-MV3] : (M1 (x) M2) x (V1 (x) V2) = (M1 x V1) (x) (M2 x V2) .
    eq [mul-MV3-1] : ((K1 x B1) (x) M2) x (K2 (x) V2) = ((K1 x B1) x K2) (x) (M2 x V2) .
    eq [mul-MV3-2] : (I (x) M2) x (K2 (x) V2) = K2 (x) (M2 x V2) .
    eq [mul-MV4] : (K1 x B1) x K2 = < (B1)^+, K2 > . K1 .
    eq [mul-MV5] : I x K = K .
    eq [mul-MV6] : (M1 x M2) x V = M1 x (M2 x V) .

    eq [mul-VCV1] : (C . V) x CV = C . (V x CV) .
    eq [mul-VCV2] : V x (C . CV) = C . (V x CV) .   
    eq [mul-VCV3] : (K (x) V) x (B (x) CV) = (K x B) (x) (V x CV) .

    eq [mul-MM1] : (C . M1) x M2 = C . (M1 x M2) .
    eq [mul-MM2] : M1 x (C . M2) = C . (M1 x M2) .
    eq [mul-MM3] : (K1 x B1) x (K2 x B2) = < (B1)^+, K2 > . (K1 x B2) .
    --- eq [mul-MM4] : (M1 (x) M2) x (M3 (x) M4) = (M1 x M3) (x) (M2 x M4) [nonexec] .
    eq [mul-MM4-1] : ((K1 x B1) (x) M1) x ((K2 x B2) (x) M2) = ((K1 x B1) x (K2 x B2)) (x) (M1 x M2) .
    eq [mul-MM4-2] : ((K1 x B1) (x) M1) x (I (x) M2) = (K1 x B1) (x) (M1 x M2) .
    eq [mul-MM4-3] : (I (x) M1) x ((K2 x B2) (x) M2) = (K2 x B2) (x) (M1 x M2) .
    eq [mul-MM4-4] : (I (x) M1) x (I (x) M2) = I (x) (M1 x M2) .
    
    --- tensor product normal forms
    eq [tensor-V1] : (C . V1) (x) V2 = C . (V1 (x) V2) .
    eq [tensor-V2] : V1 (x) (C . V2) = C . (V1 (x) V2) .

    eq [tensor-CV1] : (C . CV1) (x) CV2 = C . (CV1 (x) CV2) .
    eq [tensor-CV2] : CV1 (x) (C . CV2) = C . (CV1 (x) CV2) .
    
    eq [tensor-M1] : (C . M1) (x) M2 = C . (M1 (x) M2) .
    eq [tensor-M2] : M1 (x) (C . M2) = C . (M1 (x) M2) .
    
    --- tensor distributes over addition
    eq [dist-tensor-V1] : V1 (x) (V2 + V3) = V1 (x) V2 + V1 (x) V3 .
    eq [dist-tensor-V2] : (V1 + V2) (x) V3 = V1 (x) V3 + V2 (x) V3 .

    eq [dist-tensor-CV1] : CV1 (x) (CV2 + CV3) = CV1 (x) CV2 + CV1 (x) CV3 .
    eq [dist-tensor-CV2] : (CV1 + CV2) (x) CV3 = CV1 (x) CV3 + CV2 (x) CV3 .

    eq [dist-tensor-M1] : M1 (x) (M2 + M3) = M1 (x) M2 + M1 (x) M3 .
    eq [dist-tensor-M2] : (M1 + M2) (x) M3 = M1 (x) M3 + M2 (x) M3 .
endfm

fmod QC is
    pr MAT .

    op I : Nat -> Mat .
    op size : Vect -> Nat [memo] .
    op size : Mat  -> Nat [memo] .
    op errNat : -> [Nat] [ctor] .

    vars N N1 N2 N3 : Nat .
    vars V V1 V2 : Vect .
    vars M M1 M2 : Mat .
    vars K : Ket .
    vars B : Bra .
    vars C : Cpx .

    eq  [I1] : I x V = V .
    eq  [I2] : I(0) (x) M = M .
    eq  [I3] : M (x) I(0) = M .
    ceq [I4] : I(N) = I (x) I(N - 1) if N > 0 .

    eq  [size1.1]  : size(K) = 1 .
    eq  [size1.2]  : size(C . V) = size(V) .
    ceq [size1.3]  : size(V1 + V2) = size(V1) if size(V1) == size(V2) .
    ceq [size1.3E] : size(V1 + V2) = errNat if size(V1) =/= size(V2) .
    ceq [size1.4]  : size(M x V) = size(V) if size(M) == size(V) .
    ceq [size1.4E] : size(M x V) = errNat if size(M) =/= size(V) .
    eq  [size1.5]  : size(V1 (x) V2) = size(V1) + size(V2) .

    eq  [size2.1]  : size(K x B) = 1 .
    eq  [size2.2]  : size(C . M) = size(M) .
    ceq [size2.3]  : size(M1 + M2) = size(M1) if size(M1) == size(M2) .
    ceq [size2.3E] : size(M1 + M2) = errNat if size(M1) =/= size(M2) .
    ceq [size2.4]  : size(M1 x M2) = size(M1) if size(M1) == size(M2) .
    ceq [size2.4E] : size(M1 x M2) = errNat if size(M1) =/= size(M2) .
    eq  [size2.5]  : size(M1 (x) M2) = size(M1) + size(M2) .

    --- quantum gates
    ops P0 P1 X Y Z H S T : Nat -> Mat .
    ops CX CY CZ SWAP : Nat Nat -> Mat .
    ops CCX CCZ CSWAP : Nat Nat Nat -> Mat .

    --- single-qubit gates
    eq [P0] : P0(N) x V = (I(N) (x) |0><0| (x) I(size(V) - (N + 1))) x V .
    eq [P1] : P1(N) x V = (I(N) (x) |1><1| (x) I(size(V) - (N + 1))) x V .
    eq [X]  : X(N) x V  = (I(N) (x) (|0><1| + |1><0|) (x) I(size(V) - (N + 1))) x V .
    eq [Y]  : Y(N) x V  = (I(N) (x) ((-1 i) . |0><1| + (1 i) . |1><0|) (x) I(size(V) - (N + 1))) x V .
    eq [Z]  : Z(N) x V  = (I(N) (x) (|0><0| + (-1) . |1><1|) (x) I(size(V) - (N + 1))) x V .
    eq [H]  : H(N) x V  = (1 ./ sqrt(2) . (X(N) + Z(N))) x V .
    eq [S]  : S(N) x V  = (P0(N) + ((1 i) . P1(N))) x V .
    eq [T]  : T(N) x V  = (P0(N) + (((1 .+ (1 i)) ./ sqrt(2)) . P1(N))) x V .

    --- two-qubit gates
    eq [CX]    : CX(N1, N2) x V   = (P0(N1) + (P1(N1) x X(N2))) x V .
    eq [CY]    : CY(N1, N2) x V   = (P0(N1) + (P1(N1) x Y(N2))) x V .
    eq [CZ]    : CZ(N1, N2) x V   = (P0(N1) + (P1(N1) x Z(N2))) x V .
    eq [SWAP]  : SWAP(N1, N2) x V = (CX(N1, N2) x CX(N2, N1) x CX(N1, N2)) x V .

    --- three-qubit gates
    eq [CCX]   : CCX(N1, N2, N3) x V   = (P0(N1) + (P1(N1) x CX(N2, N3))) x V .
    eq [CCZ]   : CCZ(N1, N2, N3) x V   = (P0(N1) + (P1(N1) x CZ(N2, N3))) x V .
    eq [CSWAP] : CSWAP(N1, N2, N3) x V = (P0(N1) + (P1(N1) x SWAP(N2,N3))) x V .
endfm